;---------------------------------------
;수면약 탈지 묻기
;@Q_USE_SLEEP_POTION
;---------------------------------------
@Q_USE_SLEEP_POTION
#DIM DYNAMIC F_COUNT1
;FOR 돌리기용
#DIM DYNAMIC SLEEP_POTION_SUM
;수면제 갖고 있는 갯수의 합 - 0이면 이 함수 건너 띄기.

SLEEP_POTION_SUM = 0
FOR F_COUNT1,0,3
    SLEEP_POTION_SUM += ITEM:(1531+F_COUNT1)
NEXT
SIF SLEEP_POTION_SUM == 0
    RETURN
;만약 수면제를 하나도 갖고 있지 않다면 건너띄기.

WHILE 1
    DRAWLINE
    PRINTL
    FOR F_COUNT1,0,3
        PRINTFORML  [{F_COUNT1}]『%ITEMNAME:(1531+F_COUNT1)%』:{ITEM:(1531+F_COUNT1)}개
    NEXT
    PRINTFORML  [999] 뒤로가기
    PRINTL
    DRAWLINE
    PRINTL
    PRINTL 어떤 수면제를 사용 하시겠습니까?
    
    INPUT
    
    IF RESULT >= 0 && RESULT < 3 && ITEM:(1531+RESULT) > 0
        BREAK
    ELSEIF RESULT == 999
        PRINTL
        PRINTL
        RETURN
    ELSE
        PRINTW 사용할 수 없습니다. 다시 확인해 주십시오.
        PRINTL
        PRINTL
    ENDIF
WEND

CALL USE_SLEEP_POTION_ON_FOOD,(1531+RESULT)


;---------------------------------------
;음식에 수면약 타기
;@USE_SLEEP_POTION_ON_FOOD, SLEEP_ITEM_CODE
;---------------------------------------
@USE_SLEEP_POTION_ON_FOOD, SLEEP_ITEM_CODE
#DIM DYNAMIC SLEEP_ITEM_CODE
;수면제 아이템 넘버

ITEM:SLEEP_ITEM_CODE -= 1
FLAG:4 = (SLEEP_ITEM_CODE - 1531) + 1
;숫자가 높아질수록 효과가 좋은 수면제

PRINTL
PRINTFORML  %STR:6010%여관의 특제 비-법 소스를 넣었다.
PRINTFORML  『%ITEMNAME:(SLEEP_ITEM_CODE)%』를 사용하였습니다.
PRINTL
PRINTW

;---------------------------------------
;요리하기
;---------------------------------------
@DO_COOKING
;D:0~9 요리재료 넘버
;D:10~19 필요 요리재료 갯수

$INPUT_LOOP

DRAWLINEFORM %UNICODE(0x2500)%
CALL SHOW_MY_ITEM
DRAWLINEFORM %UNICODE(0x2500)%

PRINTL
FONTBOLD
PRINTL       주식 
FONTREGULAR
PRINTFORML [201] %ITEMNAME:201%
REPEAT COOKING_KIND-1
    PRINTFORML [{(COUNT*3)+202}] %ITEMNAME:((COUNT*3)+202)%
REND
PRINTL 
PRINTFORML    [0]그만둔다

INPUT
;CALL EVENT_MAKE_SOMETHING, 최대소지 갯수, 결과물X의 번호, '요리', 요리재료 종류
SELECTCASE RESULT
    CASE 0
        ;그만둔다
        RETURN 0
    ;주식
    CASE 201
        D = 801
        D:10 = 1
        CALL EVENT_MAKE_SOMETHING, 1, RESULT, 6003, 1
    CASE 202
        D = 802
        D:10 = 1
        CALL EVENT_MAKE_SOMETHING, 1, RESULT, 6003, 1
    CASE 205
        D = 803
        D:10 = 1
        CALL EVENT_MAKE_SOMETHING, 1, RESULT, 6003, 1
    CASE 208
        D = 803,804
        D:10 = 1,1
        CALL EVENT_MAKE_SOMETHING, 1, RESULT, 6003, 2
    CASEELSE
        GOTO INPUT_LOOP
ENDSELECT